FUNCTION /SKN/F_SW_10_06_PF_CUSTOMER.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  EXPORTING
*"     VALUE(IS_ALERT) TYPE  CHAR1
*"  TABLES
*"      T_SELECT STRUCTURE  RSSELECT
*"      T_DATA STRUCTURE  /SKN/S_SW_10_06_PF_CUSTOMER
*"----------------------------------------------------------------------
  DATA_SINGLE: SW_DEST   RFCDEST,
               LANGU  LANGU,      """langu     char2, Tanya 21/1/19
               BACKDAYS  INT4,
               DURATION  INT4.
  DATA_MULTY: PARVW PARVW,
              KUNNR KUNNR,
              VKORG VKORG,
              VTWEG VTWEG,
              SPART SPART,
              KTOKD KTOKD,
              KUKLA KUKLA,
              BUKRS BUKRS,
              ERDAT ERDAT.
  SELECT_MULTY: PARVW,
                KUNNR,
                VKORG,
                VTWEG,
                SPART,
                KTOKD,
                KUKLA,
                BUKRS.
  SELECT_SINGLE: SW_DEST, LANGU, BACKDAYS, DURATION.
  CONVERT_MULTY: PARVW ALPHA,
                 KUNNR ALPHA,
                 VKORG ALPHA,
                 VTWEG ALPHA,
                 SPART ALPHA,
                 KTOKD ALPHA,
                 KUKLA ALPHA,
                 BUKRS ALPHA.
  CONVERT_MULTY:  PARVW PARVW . """Tanya 14/11/18
  DATA: LS_ERDAT LIKE LINE OF R_ERDAT.
  IF R_ERDAT[] IS INITIAL AND LV_BACKDAYS IS NOT INITIAL.
    LS_ERDAT-SIGN  = 'I'.
    LS_ERDAT-OPTION = 'BT'.
    LS_ERDAT-LOW = SY-DATUM - LV_BACKDAYS.
    LS_ERDAT-HIGH = SY-DATUM - LV_DURATION.
    APPEND LS_ERDAT TO R_ERDAT.
  ENDIF.
  IF LV_SW_DEST IS NOT INITIAL.
    CALL FUNCTION '/SKN/FC_SW_10_06_PF_CUSTOMER'
      IMPORTING
        IS_ALERT = IS_ALERT
      TABLES
        T_SELECT = T_SELECT
        T_DATA   = T_DATA.
  ENDIF.
  CHECK LV_SW_DEST IS INITIAL.
***  CALL FUNCTION 'CONVERSION_EXIT_ISOLA_INPUT'
***    EXPORTING
***      input            = lv_langu
***    IMPORTING
***      output           = lv_langu_INT
***    EXCEPTIONS
***      unknown_language = 1
***      OTHERS           = 2.
  IF LV_LANGU IS INITIAL.
    LV_LANGU = SY-LANGU.
  ENDIF.
  CHECK R_PARVW[] IS NOT INITIAL.
  TYPES: BEGIN OF TY_PARTNER,
      PARVW TYPE TPAUM-PARVW,
      PABEZ TYPE TPAUM-PABEZ,
      VTEXT TYPE TPART-VTEXT,
    END OF TY_PARTNER,
    TT_PARTNER TYPE STANDARD TABLE OF TY_PARTNER.
  DATA: LT_PARTNER   TYPE TT_PARTNER,
        LS_PARTNER   TYPE TY_PARTNER,
        LT_PARTNER_T TYPE TT_PARTNER,
        LT_TPART     TYPE STANDARD TABLE OF TPART,
        LS_TPART     TYPE TPART,
        LT_DATA      TYPE STANDARD TABLE OF /SKN/S_SW_10_06_PF_CUSTOMER,
        LS_DATA      TYPE /SKN/S_SW_10_06_PF_CUSTOMER,
        LS_PREV      TYPE /SKN/S_SW_10_06_PF_CUSTOMER,
        LS_PARVW     LIKE LINE OF R_PARVW.
  FIELD-SYMBOLS: <LS_PARTNER> TYPE TY_PARTNER.
  SELECT TPAUM~PARVW TPAUM~PABEZ
    FROM TPAUM
    INTO TABLE LT_PARTNER
    WHERE TPAUM~SPRAS EQ LV_LANGU
    AND   TPAUM~PABEZ IN R_PARVW.
  LOOP AT R_PARVW INTO LS_PARVW.
    READ TABLE LT_PARTNER WITH KEY PABEZ = LS_PARVW-LOW
    TRANSPORTING NO FIELDS.
    IF SY-SUBRC NE 0.
      LS_PARTNER-PABEZ = LS_PARVW-LOW.
      LS_PARTNER-PARVW = LS_PARVW-LOW.
      APPEND LS_PARTNER TO LT_PARTNER.
    ENDIF.
  ENDLOOP.
  IF LT_PARTNER[] IS NOT INITIAL.
    SELECT PARVW VTEXT FROM TPART
      INTO CORRESPONDING FIELDS OF TABLE LT_TPART
      FOR ALL ENTRIES IN LT_PARTNER
      WHERE SPRAS EQ LV_LANGU
      AND   PARVW EQ LT_PARTNER-PARVW.
    SORT LT_TPART BY PARVW.
    LOOP AT LT_PARTNER ASSIGNING <LS_PARTNER>.
      READ TABLE LT_TPART INTO LS_TPART
      WITH KEY PARVW = <LS_PARTNER>-PARVW BINARY SEARCH.
      IF SY-SUBRC EQ 0.
        <LS_PARTNER>-VTEXT = LS_TPART-VTEXT.
      ENDIF.
    ENDLOOP.
    SORT LT_PARTNER BY PARVW.
  ENDIF.
  CHECK LT_PARTNER[] IS NOT INITIAL.
  SELECT KNVV~KUNNR KNVV~VKORG KNVV~VTWEG KNVV~SPART
    KNVP~PARVW
    KNA1~NAME1 KNA1~KTOKD KNA1~KUKLA
    TVKOT~VTEXT AS VTEXV TVTWT~VTEXT AS VTEXW
    TSPAT~VTEXT AS VTEXS
    T001~BUKRS T001~BUTXT
    FROM KNVV
    LEFT OUTER JOIN KNVP ON KNVP~KUNNR EQ KNVV~KUNNR
                        AND KNVP~VKORG EQ KNVV~VKORG
                        AND KNVP~VTWEG EQ KNVV~VTWEG
                        AND KNVP~SPART EQ KNVV~SPART
    INNER JOIN KNA1 ON KNA1~KUNNR EQ KNVV~KUNNR
    LEFT OUTER JOIN TVKOT ON TVKOT~SPRAS EQ LV_LANGU
                         AND TVKOT~VKORG EQ KNVV~VKORG
    LEFT OUTER JOIN TVTWT ON TVTWT~SPRAS EQ LV_LANGU
                         AND TVTWT~VTWEG EQ KNVV~VTWEG
    LEFT OUTER JOIN TSPAT ON TSPAT~SPRAS EQ LV_LANGU
                         AND TSPAT~SPART EQ KNVV~SPART
    INNER JOIN TVKO ON TVKO~VKORG EQ KNVV~VKORG
    LEFT OUTER JOIN T001 ON T001~BUKRS EQ TVKO~BUKRS
    INTO CORRESPONDING FIELDS OF TABLE LT_DATA
    WHERE KNVV~KUNNR IN R_KUNNR
    AND   KNVV~VKORG IN R_VKORG
    AND   KNVV~VTWEG IN R_VTWEG
    AND   KNVV~SPART IN R_SPART
    AND   KNVV~ERDAT IN R_ERDAT
    AND   KNA1~KTOKD IN R_KTOKD
    AND   KNA1~KUKLA IN R_KUKLA
    AND   TVKO~BUKRS IN R_BUKRS
    ORDER BY KNVV~KUNNR KNVV~VKORG KNVV~VTWEG KNVV~SPART KNVP~PARVW.
  CHECK LT_DATA IS NOT INITIAL.
  READ TABLE LT_DATA INTO LS_PREV INDEX 1.
  LT_PARTNER_T[] = LT_PARTNER[].
  LOOP AT LT_DATA INTO LS_DATA.
    IF LS_PREV-KUNNR NE LS_DATA-KUNNR
    OR LS_PREV-VKORG NE LS_DATA-VKORG
    OR LS_PREV-VTWEG NE LS_DATA-VTWEG
    OR LS_PREV-SPART NE LS_DATA-SPART.
      LOOP AT LT_PARTNER_T INTO LS_PARTNER.
        LS_PREV-PARVW = LS_PARTNER-PARVW.
        LS_PREV-VTEXT = LS_PARTNER-VTEXT.
        APPEND LS_PREV TO T_DATA.
      ENDLOOP.
      LT_PARTNER_T[] = LT_PARTNER[].
    ENDIF.
    DELETE LT_PARTNER_T WHERE PARVW EQ LS_DATA-PARVW.
    LS_PREV = LS_DATA.
  ENDLOOP.
  LOOP AT LT_PARTNER_T INTO LS_PARTNER.
    LS_PREV-PARVW = LS_PARTNER-PARVW.
    LS_PREV-VTEXT = LS_PARTNER-VTEXT.
    APPEND LS_PREV TO T_DATA.
  ENDLOOP.
  IF T_DATA[] IS NOT INITIAL.
    IS_ALERT = ABAP_TRUE.
  ENDIF.
ENDFUNCTION.