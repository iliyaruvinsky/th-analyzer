FUNCTION /SKN/F_SW_10_06_PF_VENDOR.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  EXPORTING
*"     VALUE(IS_ALERT) TYPE  CHAR1
*"  TABLES
*"      T_SELECT STRUCTURE  RSSELECT
*"      T_DATA STRUCTURE  /SKN/S_SW_10_06_PF_VENDOR
*"----------------------------------------------------------------------
  DATA_SINGLE: SW_DEST   RFCDEST,
               LANGU  LANGU,      """langu     char2, Tanya 21/1/19
               BACKDAYS  INT4,
               DURATION  INT4.
  DATA_MULTY: PARVW PARVW,
              LIFNR LIFNR,
              EKORG EKORG,
              LAND1 LAND1,
              KTOKK KTOKK,
              BUKRS BUKRS,
              ERDAT ERDAT.
  SELECT_MULTY: PARVW,
                LIFNR,
                EKORG,
                LAND1,
                KTOKK,
                BUKRS,
                ERDAT.
  SELECT_SINGLE: SW_DEST, LANGU, BACKDAYS, DURATION.
  CONVERT_MULTY: """parvw alpha,
                 LIFNR ALPHA.
 CONVERT_MULTY:  PARVW PARVW. """Tanya 14/11/18
  DATA: LS_ERDAT LIKE LINE OF R_ERDAT.
  IF R_ERDAT[] IS INITIAL AND LV_BACKDAYS IS NOT INITIAL.
    LS_ERDAT-SIGN  = 'I'.
    LS_ERDAT-OPTION = 'BT'.
    LS_ERDAT-LOW = SY-DATUM - LV_BACKDAYS.
    LS_ERDAT-HIGH = SY-DATUM - LV_DURATION.
    APPEND LS_ERDAT TO R_ERDAT.
  ENDIF.
  IF LV_SW_DEST IS NOT INITIAL.
    CALL FUNCTION '/SKN/FC_SW_10_06_PF_VENDOR'
      IMPORTING
        IS_ALERT = IS_ALERT
      TABLES
        T_SELECT = T_SELECT
        T_DATA   = T_DATA.
  ENDIF.
  CHECK LV_SW_DEST IS INITIAL.
***  CALL FUNCTION 'CONVERSION_EXIT_ISOLA_INPUT'
***    EXPORTING
***      input            = lv_langu
***    IMPORTING
***      output           = lv_langu_INT
***    EXCEPTIONS
***      unknown_language = 1
***      OTHERS           = 2.
  IF LV_LANGU IS INITIAL.
    LV_LANGU = SY-LANGU.
  ENDIF.
  CHECK R_PARVW[] IS NOT INITIAL.
  TYPES: BEGIN OF TY_PARTNER,
      PARVW TYPE TPAUM-PARVW,
      PABEZ TYPE TPAUM-PABEZ,
      VTEXT TYPE TPART-VTEXT,
    END OF TY_PARTNER,
    TT_PARTNER TYPE STANDARD TABLE OF TY_PARTNER.
  DATA: LT_PARTNER   TYPE TT_PARTNER,
        LS_PARTNER   TYPE TY_PARTNER,
        LT_PARTNER_T TYPE TT_PARTNER,
        LT_TPART     TYPE STANDARD TABLE OF TPART,
        LS_TPART     TYPE TPART,
        LT_DATA      TYPE STANDARD TABLE OF /SKN/S_SW_10_06_PF_VENDOR,
        LT_DATA2     TYPE STANDARD TABLE OF /SKN/S_SW_10_06_PF_VENDOR,
        LS_DATA      TYPE /SKN/S_SW_10_06_PF_VENDOR,
        LS_PREV      TYPE /SKN/S_SW_10_06_PF_VENDOR,
        LS_PARVW     LIKE LINE OF R_PARVW.
  FIELD-SYMBOLS: <LS_PARTNER> TYPE TY_PARTNER.
  SELECT TPAUM~PARVW TPAUM~PABEZ
    FROM TPAUM
    INTO TABLE LT_PARTNER
    WHERE TPAUM~SPRAS EQ LV_LANGU
    AND   TPAUM~PABEZ IN R_PARVW.
  LOOP AT R_PARVW INTO LS_PARVW.
    READ TABLE LT_PARTNER WITH KEY PABEZ = LS_PARVW-LOW
    TRANSPORTING NO FIELDS.
    IF SY-SUBRC NE 0.
      LS_PARTNER-PABEZ = LS_PARVW-LOW.
      LS_PARTNER-PARVW = LS_PARVW-LOW.
      APPEND LS_PARTNER TO LT_PARTNER.
    ENDIF.
  ENDLOOP.
  IF LT_PARTNER[] IS NOT INITIAL.
    SELECT PARVW VTEXT FROM TPART
      INTO CORRESPONDING FIELDS OF TABLE LT_TPART
      FOR ALL ENTRIES IN LT_PARTNER
      WHERE SPRAS EQ LV_LANGU
      AND   PARVW EQ LT_PARTNER-PARVW.
    SORT LT_TPART BY PARVW.
    LOOP AT LT_PARTNER ASSIGNING <LS_PARTNER>.
      READ TABLE LT_TPART INTO LS_TPART
      WITH KEY PARVW = <LS_PARTNER>-PARVW BINARY SEARCH.
      IF SY-SUBRC EQ 0.
        <LS_PARTNER>-VTEXT = LS_TPART-VTEXT.
      ENDIF.
    ENDLOOP.
    SORT LT_PARTNER BY PARVW.
  ENDIF.
  CHECK LT_PARTNER[] IS NOT INITIAL.
  SELECT LFM1~LIFNR LFM1~EKORG WYT3~LTSNR WYT3~WERKS
    WYT3~PARVW
    LFA1~NAME1 LFA1~LAND1 LFA1~KTOKK
    T024E~EKOTX T024E~BUKRS T001~BUTXT
    FROM LFM1
    LEFT OUTER JOIN WYT3 ON WYT3~LIFNR  EQ LFM1~LIFNR
                        AND WYT3~EKORG  EQ LFM1~EKORG
*                        AND wyt3~ltsnr  EQ space
*                        AND wyt3~werks  EQ space
    INNER JOIN LFA1      ON LFA1~LIFNR  EQ LFM1~LIFNR
    INNER JOIN T024E     ON T024E~EKORG EQ LFM1~EKORG
    LEFT OUTER JOIN T001 ON T001~BUKRS  EQ T024E~BUKRS
    INTO CORRESPONDING FIELDS OF TABLE LT_DATA
    WHERE LFM1~LIFNR IN R_LIFNR
    AND   LFM1~EKORG IN R_EKORG
    AND   LFA1~LAND1 IN R_LAND1
    AND   LFA1~KTOKK IN R_KTOKK
    AND   LFA1~ERDAT IN R_ERDAT
    AND   T024E~BUKRS IN R_BUKRS.
*    ORDER BY lfm1~lifnr lfm1~ekorg wyt3~parvw.
  DELETE LT_DATA WHERE LTSNR IS NOT INITIAL OR WERKS IS NOT INITIAL.
  SELECT LFM2~LIFNR LFM2~EKORG LFM2~LTSNR LFM2~WERKS
    WYT3~PARVW
    LFA1~NAME1 LFA1~LAND1 LFA1~KTOKK
    T024E~EKOTX T024E~BUKRS T001~BUTXT
    FROM LFM2
    LEFT OUTER JOIN WYT3 ON WYT3~LIFNR  EQ LFM2~LIFNR
                        AND WYT3~EKORG  EQ LFM2~EKORG
                        AND WYT3~LTSNR  EQ LFM2~LTSNR
                        AND WYT3~WERKS  EQ LFM2~WERKS
    INNER JOIN LFA1      ON LFA1~LIFNR  EQ LFM2~LIFNR
    INNER JOIN T024E     ON T024E~EKORG EQ LFM2~EKORG
    LEFT OUTER JOIN T001 ON T001~BUKRS  EQ T024E~BUKRS
    INTO CORRESPONDING FIELDS OF TABLE LT_DATA2
    WHERE LFM2~LIFNR IN R_LIFNR
    AND   LFM2~EKORG IN R_EKORG
    AND   LFA1~LAND1 IN R_LAND1
    AND   LFA1~KTOKK IN R_KTOKK
    AND   LFA1~ERDAT IN R_ERDAT
    AND   T024E~BUKRS IN R_BUKRS.
*    ORDER BY lfm2~lifnr lfm2~ekorg lfm2~ltsnr lfm2~werks wyt3~parvw.
  APPEND LINES OF LT_DATA2 TO LT_DATA.
  SORT LT_DATA BY LIFNR EKORG LTSNR WERKS PARVW.
  CHECK LT_DATA IS NOT INITIAL.
  READ TABLE LT_DATA INTO LS_PREV INDEX 1.
  LT_PARTNER_T[] = LT_PARTNER[].
  LOOP AT LT_DATA INTO LS_DATA.
    IF LS_PREV-LIFNR NE LS_DATA-LIFNR
    OR LS_PREV-EKORG NE LS_DATA-EKORG
    OR LS_PREV-LTSNR NE LS_DATA-LTSNR
    OR LS_PREV-WERKS NE LS_DATA-WERKS.
      LOOP AT LT_PARTNER_T INTO LS_PARTNER.
        LS_PREV-PARVW = LS_PARTNER-PARVW.
        LS_PREV-VTEXT = LS_PARTNER-VTEXT.
        APPEND LS_PREV TO T_DATA.
      ENDLOOP.
      LT_PARTNER_T[] = LT_PARTNER[].
    ENDIF.
    DELETE LT_PARTNER_T WHERE PARVW EQ LS_DATA-PARVW.
    LS_PREV = LS_DATA.
  ENDLOOP.
  LOOP AT LT_PARTNER_T INTO LS_PARTNER.
    LS_PREV-PARVW = LS_PARTNER-PARVW.
    LS_PREV-VTEXT = LS_PARTNER-VTEXT.
    APPEND LS_PREV TO T_DATA.
  ENDLOOP.
  IF T_DATA[] IS NOT INITIAL.
    IS_ALERT = ABAP_TRUE.
  ENDIF.
ENDFUNCTION.